	SUBROUTINE MUSK_NL(QMX1,QMX2,QJX1,QJX2,NTRECH,NTC,C1,C2,C3)
	!ROTINA QUE CALCULA MUSKINGUM CUNGE NAO LINEAR
	USE VARS_MAIN
	IMPLICIT NONE
	INTEGER ITC,ITR
	!COEFICIENTES MUSKINGUM
	REAL C1,C2,C3
	!VARIAVEIS AUXILIARES
	REAL QMX1,QMX2,QJX1,QJX2
	INTEGER NTRECH,NTC !NUMERO DE SUBTRECHOS, NUMERO DE SUBINTERVALOS
	!VAZOES INTERNAS DO TRECHO	
	REAL,ALLOCATABLE:: QC(:,:)
	REAL QMUSKNL,BMUSKNL,CELMUSKNL
	REAL DTCAL,DX,AK,XXX,DEN
	REAL QPERDAS
	ALLOCATE (QC(NTRECH+1,NTC+1))

	IF(NTRECH>NUMUSK) STOP 'MUITOS SUBTRECHOS NA ROTINA MUSK'

	QC=0.0 !ZERA MATRIZ
	QCONTORJ(IC,1)=QJX1


	QPERDAS=0.0

	!CONDICOES DE CONTORNO DE MONTANTE	
	DO ITC=1,NTC+1	  !RETA ENTRE QMX1 E QMX2
		!QC(1,ITC)=QMX1+(QMX2-QMX1)*(ITC-1)/NTC
		QC(1,ITC)=MAX(QCONTORM(IC,ITC)-EVQ(IC)-QPERDAS,0.0) !MODIFICADO PARA RETIRAR EVAPORAÇÃO DIRETA DAS SUPERFICIES LÍQUIDAS
	ENDDO

	!CONDICOES INICIAIS	
	DO ITR=1,NTRECH+1
		!QC(ITR,1)=QMX1+(QJX1-QMX1)*(ITR-1)/NTRECH
		QC(ITR,1)=QRIOINI(IC,ITR)
	ENDDO

	DO ITC=1,NTC
		DO ITR=1,NTRECH


			!AQUI ESTÁ A DIFERENCA ENTRE LINEAR E NAO LINEAR
			DTCAL=DT(IC)
			DX=SRIO(IC)*1000./NSUBT(IC)
			QMUSKNL=QC(ITR+1,ITC) !CONSIDERA VÁLIDA A ÚLTIMA VAZAO CALCULADA
			CALL INTERPMUSK(QMUSKNL,CELMUSKNL,BMUSKNL)

			AK=DX/CELMUSKNL
			XXX=0.5*(1.0-(QMUSKNL/(BMUSKNL*DECL(IC)*CELMUSKNL*DX)))
			DEN=2.*AK*(1.-XXX)+DTCAL
			C1=(2.*AK*XXX+DTCAL)/DEN
			C2=(DTCAL-2.*AK*XXX)/DEN
			C3=(2.*AK*(1.-XXX)-DTCAL)/DEN
			!


			QC(ITR+1,ITC+1)=C1*QC(ITR,ITC)+C2*QC(ITR,ITC+1)+C3*QC(ITR+1,ITC)
			QC(ITR+1,ITC+1)=MAX(QC(ITR+1,ITC+1),0.0) !EVITA VAZÕES NEGATIVAS
		ENDDO
		QCONTORJ(IC,ITC+1)=QC(NTRECH+1,ITC+1) !GUARDA HIDROGRAMA DE SAÍDA COMPLETO
	ENDDO

	!GUARDA VALORES DA CONDIÇÃO INICIAL PARA A PROXIMA VEZ QUE VAI CALCULAR MUSKINGUM CUNGE
	QRIOINI(IC,1)=QCONTORM(IC,NTC+1)
	DO ITR=2,NTRECH+1
		QRIOINI(IC,ITR)=QC(ITR,NTC+1)
	ENDDO

	!GUARDA VALOR DO FINAL DO PERÍODO DE PROPAGAÇÃO NO EXTREMO DE JUSANTE DO TRECHO
	QJX2=QC(NTRECH+1,NTC+1) !ESTE VALOR VOLTA PARA PROGRAMA PRINCIPAL

	DEALLOCATE (QC)
	RETURN
	END